apply plugin: 'com.android.library'
apply plugin: 'com.diffplug.gradle.spotless'

android {
    compileSdkVersion 28


    defaultConfig {

        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.1"
        ndk.abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        externalNativeBuild {
            cmake {
                // The covert library requires C++17 features.
                cppFlags "-std=c++1z -fexceptions"

                // Build the covert library.
                targets "covert"

                // The c++_{static,shared} STL has full support of C++17 features.
                // Also, force the CMake defined C++ standards.
                //
                // WARNING: This option will only work for CMake version ~> 3.8,
                // which is not the one bundled with the SDK. An external CMake
                // binary has to be provided, down below.
                arguments "-DANDROID_STL=c++_shared",
                          "-DANDROID_TOOLCHAIN=clang",
                          "-DANDROID_ARM_MODE=arm",
                          "-DCMAKE_CXX_STANDARD=17",
                          "-DCMAKE_CXX_STANDARD_REQUIRED=ON",
                          "-DCMAKE_CXX_EXTENSIONS=OFF"
            }
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    externalNativeBuild {
        cmake {
            // Do not use the bundled CMake, use a newer one.
            version "3.15.0-rc2"
            // The path to the master CMake configuration, relative to the
            // path of this file.
            path "CMakeLists.txt"
        }
    }
}

spotless {
    java {
        removeUnusedImports()
        target '**/*.java'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0-rc02'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
